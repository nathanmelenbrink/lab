#include <WiFi.h>
#include <AsyncTCP.h>
#include <ESPAsyncWebServer.h>
#include <AccelStepper.h>

//AccelStepper stepper2(AccelStepper::FULL4WIRE, 11, 12, 13, 14);
//AccelStepper stepper1(AccelStepper::FULL4WIRE, 19, 20, 21, 33);

// Define a stepper and the pins it will use
AccelStepper stepper_X1(1, 9, 10); // initialise accelstepper for a two wire board
AccelStepper stepper_X2(1, 11, 12); // initialise accelstepper for a two wire board
AccelStepper stepper_Y(1, 13, 14); // initialise accelstepper for a two wire board

// Replace with your network credentials
const char* ssid = "MAKERSPACE";
const char* password = "12345678";

String message = "";

// Create AsyncWebServer object on port 80
AsyncWebServer server(80);
AsyncWebSocket ws("/ws");

//Variables to save values from HTML form
String steps_X;
String steps_Y;
bool newRequest = false;


const char index_html[] PROGMEM = R"rawliteral(
<!DOCTYPE html>
<html>
<head>
  <title>ESP32 Machine Control</title>
  <meta name="viewport" content="width=device-width, initial-scale=1">
</head>
<body>
  <div class="topnav">
    <h3>ESP32 Machine Control</h3>
  </div>
  <div class="content">
        <form>
          <label for="steps_x">X Position:</label>
          <input type="number" id="steps_x" name="steps_x">
          <br><br>
          <label for="steps_y">Y Position:</label>
          <input type="number" id="steps_y" name="steps_y">
        </form>
        <br> 
        <button onclick="submitForm()">GO!</button>
  </div>
</body>
<script>
var gateway = `ws://${window.location.hostname}/ws`;
var websocket;
window.addEventListener('load', onload);
var dir;

function onload(event) {
    initWebSocket();
}

function initWebSocket() {
    console.log('Trying to open a WebSocket connectionâ€¦');
    websocket = new WebSocket(gateway);
    websocket.onopen = onOpen;
    websocket.onclose = onClose;
    websocket.onmessage = onMessage;
}

function onOpen(event) {
    console.log('Connection opened');
}

function onClose(event) {
    console.log('Connection closed');
    setTimeout(initWebSocket, 2000);
}

function submitForm(){
    var steps_x = document.getElementById("steps_x").value;
    var steps_y = document.getElementById("steps_y").value;
    websocket.send(steps_x+"&"+steps_y);
}

function onMessage(event) {
    console.log(event.data);
}
</script>
</html>
)rawliteral";

// Initialize WiFi
void initWiFi() {
  WiFi.mode(WIFI_STA);
  WiFi.begin(ssid, password);
  Serial.print("Connecting to WiFi ..");
  while (WiFi.status() != WL_CONNECTED) {
    Serial.print('.');
    delay(1000);
  }
  Serial.println(WiFi.localIP());
}

void handleWebSocketMessage(void *arg, uint8_t *data, size_t len) {
  AwsFrameInfo *info = (AwsFrameInfo*)arg;
  if (info->final && info->index == 0 && info->len == len && info->opcode == WS_TEXT) {
    data[len] = 0;
    message = (char*)data;
    steps_X = message.substring(0, message.indexOf("&"));
    steps_Y = message.substring(message.indexOf("&") + 1, message.length());
    Serial.print("steps_X: ");
    Serial.println(steps_X);
    Serial.print("steps_Y: ");
    Serial.println(steps_Y);
    newRequest = true;
  }
}

void onEvent(AsyncWebSocket *server, AsyncWebSocketClient *client, AwsEventType type, void *arg, uint8_t *data, size_t len) {
  switch (type) {
    case WS_EVT_CONNECT:
      Serial.printf("WebSocket client #%u connected from %s\n", client->id(), client->remoteIP().toString().c_str());
      //Notify client of motor current state when it first connects
      break;
    case WS_EVT_DISCONNECT:
      Serial.printf("WebSocket client #%u disconnected\n", client->id());
      break;
    case WS_EVT_DATA:
      handleWebSocketMessage(arg, data, len);
      break;
    case WS_EVT_PONG:
    case WS_EVT_ERROR:
      break;
  }
}

void initWebSocket() {
  ws.onEvent(onEvent);
  server.addHandler(&ws);
}

String processor(const String& var) {
  Serial.println("PROCESSOR");
  Serial.println(var);
  if (var == "STATE") {
    return "OFF";
  }
  return String();
}

void setup() {
  // Serial port for debugging purposes
  Serial.begin(115200);
  initWiFi();
  initWebSocket();

  // Route for root / web page
  server.on("/", HTTP_GET, [](AsyncWebServerRequest * request) {
    request->send_P(200, "text/html", index_html, processor);
  });

  server.begin();

  stepper_X1.setMaxSpeed(1000.0);
  stepper_X1.setAcceleration(1000.0);

  stepper_X2.setMaxSpeed(1000.0);
  stepper_X2.setAcceleration(1000.0);

  stepper_Y.setMaxSpeed(1000.0);
  stepper_Y.setAcceleration(1000.0);
}

void loop() {
  if (newRequest) {
    stepper_X1.moveTo(steps_X.toInt());
    stepper_X2.moveTo(steps_X.toInt());
    stepper_Y.moveTo(steps_Y.toInt());
    Serial.println("new");
    Serial.println(steps_X.toInt());
    newRequest = false;
    ws.cleanupClients();
  }

  if (stepper_X1.distanceToGo() == 0 && stepper_Y.distanceToGo() == 0) {
    Serial.println("done");
  }

  stepper_X1.run();
  stepper_X2.run();
  stepper_Y.run();
  
}